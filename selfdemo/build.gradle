plugins {
    id 'com.android.application'
}

def arengine_libpath = "${projectDir}/src/main/libs/"
// Create a configuration to mark which aars to extract .so files from
configurations { natives }

android {
    namespace 'com.example.selfdemo'
    compileSdk 33

    defaultConfig {
        minSdk 30
        targetSdk 33

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags "-std=c++17", "-Wall"
                arguments "-DANDROID_STL=c++_static",
                        "-DARENGINE_LIBPATH=${arengine_libpath}"
            }
        }
        ndk {
            abiFilters "armeabi-v7a","arm64-v8a"
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.22.1"
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    ndkVersion '23.1.7779620'
    buildToolsVersion '33.0.1'
}

dependencies {

    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'androidx.activity:activity:1.0.0'
    implementation 'com.huawei.hms:arenginesdk:4.0.0.5'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    natives 'com.huawei.hms:arenginesdk:4.0.0.5'
}

// Extracts the shared libraries from aars in the natives configuration.
// This is done so that NDK builds can access these libraries.
task extractNativeLibrariesArEngine() {
    outputs.upToDateWhen { false }
    doFirst {
        configurations.natives.files.each { f ->
            copy {
                from zipTree(f)
                into arengine_libpath
                include "jni/**/*"
            }
        }
    }
}

tasks.whenTaskAdded {
    task-> if (task.name.contains("external") && !task.name.contains("Clean")) {
        task.dependsOn(extractNativeLibrariesArEngine)
    }
}